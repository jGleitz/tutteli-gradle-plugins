buildscript {
    ext {
        junit_version = '5.0.3'
        junit_gradle_version = '1.0.3'
        publish_version = '0.9.10'
    }

    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:$publish_version"
        classpath "org.junit.platform:junit-platform-gradle-plugin:$junit_gradle_version"
    }
}


buildscript {
    ext.error = false
    if (!ext.error) {
        apply from: "${rootProject.projectDir}/gradle/scripts/localRepo.gradle"
        includeLocalRepo(gradle.ext.repo, 'jacoco', gradle.ext.version, this)
    }
}

version = gradle.ext.version
group = 'ch.tutteli'

subprojects { subproject ->
    apply plugin: 'groovy'
    apply plugin: 'java-gradle-plugin'
    apply plugin: "com.gradle.plugin-publish"
    if (error || !subproject.localPluginExists) {
        apply plugin: 'org.junit.platform.gradle.plugin'
    } else {
        apply plugin: 'ch.tutteli.jacoco'
        def report = subproject.tasks.getByName('junitPlatformJacocoReport')
    }

    version = rootProject.version
    group = rootProject.group

    repositories {
        jcenter()
    }

    //TODO can be removed if plugin DSL is also applicable to settings.gradle
    task createClasspathManifest {
        def outputDir = file("${buildDir}/${name}")

        inputs.files sourceSets.main.runtimeClasspath
        outputs.dir outputDir

        doLast {
            outputDir.mkdirs()
            file("${outputDir}/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
        }
    }
    test.dependsOn(createClasspathManifest)

    dependencies {
        testCompile("org.junit.jupiter:junit-jupiter-api:$junit_version")
        testRuntime("org.junit.jupiter:junit-jupiter-engine:$junit_version")
        testRuntime files(createClasspathManifest)
    }

    afterEvaluate {
        gradlePlugin {
            plugins {
                tutteliSettings {
                    id = subproject.ext.plugin_id
                    implementationClass = subproject.ext.plugin_class
                }
            }
        }

        pluginBundle {
            website = 'https://github.com/robstoll/tutteli-gradle'
            vcsUrl = 'https://github.com/robstoll/tutteli-gradle'

            plugins {
                tutteliSettings {
                    id = subproject.ext.plugin_id
                    displayName = subproject.ext.plugin_name
                    description = subproject.ext.plugin_description
                    tags = subproject.ext.plugin_tags
                }
            }

            mavenCoordinates {
                groupId = subproject.group
            }
        }
    }


    task repo(type: Copy) {
        def outputJar = "${gradle.ext.repo}/ch/tutteli/${project.name}/${project.version}/"
        outputs.dir outputJar
        from jar
        into outputJar
    }
}

task cleanRepo(type: Delete) {
    delete gradle.ext.repo
}

/*
Release & deploy
----------------
1. Update master:
    a) point to the tag
        1) search for the old version and replace with new (e.g. search=1.0.0, replace=1.1.0)
        2) search for `branch=master` and replace it with `tag=vX.Y.Z` (build status and coverage in README.md)
        3) search for `tree/master` and replace it with `tree/vX.Y.Z` (README.md)
    b) change `version` in settings.gradle to X.Y.Z (remove -SNAPSHOT)
    c) commit (modified build.gradle, README.md and differences.md)
    d) git tag vX.Y.Z
    e) git push origin vX.Y.Z
2. publish plugins
   a) gr publishPlugins
3. create release on github


Prepare next dev cycle
-----------------------
1. change version in settings.gradle to X.Y.Z-SNAPSHOT
2. point to master
   a) search for `tag=vX.Y.Z` and replace it with `branch=master`
   b) search for `tree/vX.Y.Z` and replace it with `tree/master`
3. commit & push changes

*/
