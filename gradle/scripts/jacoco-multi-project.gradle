apply plugin: 'kotlin'
apply plugin: 'jacoco'

jacoco {
    toolVersion = "$jacoco_tool_version"
}

task jacocoMerge(type: JacocoMerge) {
    jacocoMulti.jacocoProjects.each {
        executionData "${it.buildDir}/jacoco/junitPlatformTest.exec"
    }
}

task codeCoverageReport(type: JacocoReport) {
    dependsOn jacocoMulti.sourceProjects.build
    dependsOn jacocoMerge
    jacocoMerge.mustRunAfter jacocoMulti.sourceProjects.build

    executionData jacocoMerge.destinationFile

    sourceDirectories = files()
    classDirectories = files()

    jacocoMulti.sourceProjects.each {
        sourceDirectories += it.sourceSets.main.allJava.sourceDirectories
        classDirectories += files(it.sourceSets.main.output.classesDirs)
    }
    doFirst {
        logger.info("going to generate merged jacoco report based on:")
        logger.info("  - source projects: ${jacocoMulti.sourceProjects*.name}")
        logger.info("  - jacoco projects: ${jacocoMulti.jacocoProjects*.name}")
        logger.info("=========================================")
    }

    reports {
        csv.enabled = false
        html.enabled = false
        html.destination file("${buildDir}/reports/jacoco/html/")
        xml.enabled = false
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
    }
}
